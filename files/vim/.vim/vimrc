" Author: Matt Soucy

" vim settings {{{
set laststatus=2
set tabstop=4
set shiftwidth=4
set number
set ignorecase
set modeline
set nobackup
set noswapfile
set wrap
set hidden
set hlsearch
if exists('&cc')
	set cc=80,120
endif
set cursorline cursorcolumn
set shortmess+=I " Don't show the intro message
syntax on "Syntax highlighting
" }}}

" Plugins {{{
" Load plugins {{{
if !empty(globpath(&rtp, "autoload/plug.vim"))
	call plug#begin()
	for f in globpath(&rtp, 'pluglists/*.vimplug', 0, 1)
		exe 'source' f
	endfor
	call plug#end()
endif

filetype plugin indent on
syntax on
" }}}

" Plugin settings {{{
let g:table_mode_corner = '|'
let g:vim_markdown_folding_disabled=1
let g:netrw_liststyle=3
let g:haskell_conceal_wide=1
" Clang-format {{{
let g:clang_format#auto_format = 0
let g:clang_format#code_style = 'llvm'
let g:clang_format#style_options = {
			\ 'IndentWidth': 4,
			\ 'TabWidth': 4,
			\ 'UseTab': "Always",
			\ 'AlwaysBreakTemplateDeclarations': "true",
			\ 'ColumnLimit': 80
			\}
let g:clang_format#auto_formatexpr = 1
" }}}
" Syntastic {{{
let g:syntastic_cpp_config_file = ".syntastic"
let g:syntastic_always_populate_loc_list = 1
let g:syntastic_check_on_wq = 0
let g:syntastic_aggregate_errors = 1
let g:syntastic_d_checkers = ['dub']
let g:syntastic_mode_map = {
        \ "mode": "active",
        \ "active_filetypes": [],
        \ "passive_filetypes": ["d"] }
" }}}
" tmuxline {{{
let g:tmuxline_powerline_separators = 0
let g:tmuxline_preset = {
      \'a'    : '#S',
      \'b'    : '#W',
      \'c'    : '#H',
      \'win'  : '#I | #W',
      \'cwin' : '#I | #W',
      \'y'    : '%a',
      \'z'    : '%R'}

" }}}

" }}}

" }}} Plugins

" Color {{{
set background=dark
colorscheme desert " Use desert if badwolf isn't installed yet
if has("gui_running") || &t_Co == 88 || &t_Co == 256
	silent! colorscheme badwolf
endif
" }}}

" Keybindings {{{
"Mapped some FUNCTION keys to be more useful
map <F9> :make<Return>:copen<Return><Return>
map <F10> :cprevious<Return>
map <F11> :cnext<Return>

" paste mode toggle (needed when using autoindent/smartindent)
map <F7> :set paste<CR>
map <F8> :set nopaste<CR>
imap <F7> <C-O>:set paste<CR>
imap <F8> <C-O>:set nopaste<CR>
set pastetoggle=<F12>

"This is a nice buffer switcher
nnoremap <F5> :buffers<CR>:buffer<Space>

imap jj <ESC>
nmap <silent> <Leader>/ :nohlsearch<CR>
nmap <silent> <Leader>l :lclose<CR>
nmap <silent> <Leader>r :so $MYVIMRC<CR>

" Move by row, not line
nnoremap j gj
nnoremap k gk
nnoremap gj j
nnoremap gk k

" Highlight line and column
nnoremap H :set cursorline! cursorcolumn!<CR>

if exists(":TagbarToggle")
	nmap <silent> <Leader>t :TagbarToggle<CR>
endif
if exists(":Unite")
	nnoremap <C-p> :Unite file_rec/async<CR>
	nnoremap <space>/ :Unite grep:.<cr>
	if executable('ag')
        let g:unite_source_grep_command='ag'
        let g:unite_source_grep_default_opts='--nocolor --line-numbers --nogroup -S -C4'
        let g:unite_source_grep_recursive_opt=''
	elseif executable('ack')
        let g:unite_source_grep_command='ack'
        let g:unite_source_grep_default_opts='--no-heading --no-color -C4'
        let g:unite_source_grep_recursive_opt=''
	endif
endif
" }}}

" Completion {{{
"Set a nice Omnifunc - <CTRL>X <CTRL>O
set ofu=syntaxcomplete#Complete

" Make vim popup behave more like an IDE POPUP
set completeopt=longest,menuone
" }}}

" Lightline controls {{{
let g:lightline = {
      \ 'colorscheme': 'badwolf',
      \ 'active': {
      \   'left': [ [ 'mode', 'paste' ], [ 'readonly', 'modified', 'fugitive', 'filename' ], ['ctrlpmark'] ],
      \   'right': [ [ 'syntastic', 'lineinfo' ], ['percent'], [ 'fileformat', 'fileencoding', 'filetype' ] ]
      \ },
      \ 'component': {
      \   'modified': '%{&filetype=="help"?"":&modified?"+":&modifiable?"":"-"}'
      \ },
      \ 'component_function': {
      \   'fugitive': 'fugitive#head',
      \   'ctrlpmark': 'CtrlPMark',
	  \   'modified': 'MyModified',
	  \   'readonly': 'MyReadonly',
	  \   'fileformat': 'MyFileformat',
      \   'filetype': 'MyFiletype',
      \   'fileencoding': 'MyFileencoding',
      \ },
      \ 'component_visible_condition': {
      \   'readonly': '(&filetype!="help"&& &readonly)',
      \   'modified': '(&filetype!="help"&&(&modified||!&modifiable))'
      \ },
      \ 'component_expand': {
      \   'syntastic': 'SyntasticStatuslineFlag',
      \ },
      \ 'component_type': {
      \   'syntastic': 'error',
      \ },
      \ }

function! MyModified()
  return &ft =~ 'help' ? '' : &modified ? '+' : &modifiable ? '' : '-'
endfunction

function! MyReadonly()
  return &ft !~? 'help' && &readonly ? 'RO' : ''
endfunction

function! MyFileformat()
  return winwidth(0) > 70 ? &fileformat : ''
endfunction

function! MyFiletype()
  return winwidth(0) > 70 ? (strlen(&filetype) ? &filetype : 'no ft') : ''
endfunction

function! MyFileencoding()
  return winwidth(0) > 70 ? (strlen(&fenc) ? &fenc : &enc) : ''
endfunction

function! MyFugitive()
  try
    if expand('%:t') !~? 'Tagbar\|Gundo\|NERD' && exists('*fugitive#head')
      let mark = ''  " edit here for cool mark
      let _ = fugitive#head()
      return strlen(_) ? mark._ : ''
    endif
  catch
  endtry
  return ''
endfunction

function! CtrlPMark()
  if expand('%:t') =~ 'ControlP'
    call lightline#link('iR'[g:lightline.ctrlp_regex])
    return lightline#concatenate([g:lightline.ctrlp_prev, g:lightline.ctrlp_item
          \ , g:lightline.ctrlp_next], 0)
  else
    return ''
  endif
endfunction

let g:ctrlp_status_func = {
  \ 'main': 'CtrlPStatusFunc_1',
  \ 'prog': 'CtrlPStatusFunc_2',
  \ }

function! CtrlPStatusFunc_1(focus, byfname, regex, prev, item, next, marked)
  let g:lightline.ctrlp_regex = a:regex
  let g:lightline.ctrlp_prev = a:prev
  let g:lightline.ctrlp_item = a:item
  let g:lightline.ctrlp_next = a:next
  return lightline#statusline(0)
endfunction

function! CtrlPStatusFunc_2(str)
  return lightline#statusline(0)
endfunction

let g:tagbar_status_func = 'TagbarStatusFunc'

function! TagbarStatusFunc(current, sort, fname, ...) abort
    let g:lightline.fname = a:fname
  return lightline#statusline(0)
endfunction

if exists(":SyntasticCheck")
  augroup AutoSyntastic
    autocmd!
    autocmd BufWritePost *.c,*.cpp call s:syntastic()
  augroup END
  function! s:syntastic()
    SyntasticCheck
    call lightline#update()
  endfunction
endif
" }}}

" Custom term behavior {{{
if &term =~ '^screen'
    " tmux will send xterm-style keys when its xterm-keys option is on
    execute "set <xUp>=\e[1;*A"
    execute "set <xDown>=\e[1;*B"
    execute "set <xRight>=\e[1;*C"
    execute "set <xLeft>=\e[1;*D"
endif
" }}}

if filereadable(glob("~/.vimrc.local"))
    source ~/.vimrc.local
endif

" vim: foldmethod=marker
